dependencies {
	compile project(':language')
	compile project(':language-ide')
	compile "org.eclipse.xtext:org.eclipse.xtext.xbase.web:${xtextVersion}"
	compile "org.eclipse.xtext:org.eclipse.xtext.web.servlet:${xtextVersion}"
	compile "org.eclipse.xtend:org.eclipse.xtend.lib:${xtextVersion}"
	compile "org.eclipse.jetty:jetty-server:9.4.42.v20210604"
	compile "org.eclipse.jetty:jetty-annotations:9.4.42.v20210604"
	compile "org.slf4j:slf4j-simple:1.7.31"
}

def webpackOutputDir = "${buildDir}/webpack"
def productionResources = "${webpackOutputDir}/production"
def mainClass = 'org.eclipse.viatra.solver.language.web.ServerLauncher'

apply plugin: 'com.moowork.node'

for (environment in ['production', 'development']) {
	def taskName = 'webpack' + environment.substring(0, 1).toUpperCase() + environment.substring(1);
	task(taskName, type: NpmTask) {
		dependsOn ':language:generateXtext'
		inputs.dir 'src/main/css'
		inputs.dir 'src/main/html'
		inputs.dir 'src/main/js'
		inputs.dir "${buildDir}/generated/sources/xtext/js"
		inputs.file 'webpack.config.js'
		outputs.dir "${webpackOutputDir}/${environment}"
		args = ['run', 'build']
		setEnvironment NODE_ENV: environment
	}
}

apply plugin: 'application'
mainClassName = mainClass
distZip.enabled = false

jar {
	dependsOn webpackProduction
	from(productionResources) {
		into 'webapp'
	}
}

apply plugin: 'com.github.johnrengelman.shadow'
shadowDistZip.enabled = false

shadowJar {
	dependsOn webpackProduction
	from(project.convention.getPlugin(JavaPluginConvention).sourceSets.main.output)
	configurations = [project.configurations.runtime]
	exclude('META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA','schema/*',
		'.options', '.api_description', '*.profile', 'about.*', 'about_*.html', 'about_files/*',
		'plugin.xml', 'systembundle.properties', 'profile.list', 'META-INF/resources/xtext/**')
	append('plugin.properties')
	from(productionResources) {
		into 'webapp'
	}
}

task jettyRun(type: JavaExec) {
	dependsOn webpackDevelopment
	dependsOn sourceSets.main.runtimeClasspath
	classpath = sourceSets.main.runtimeClasspath.filter{it.exists()}
	main = mainClass
	standardInput = System.in
	group = 'run'
	description = 'Starts an example Jetty server with your language'
	environment(
		DEV_MODE: 'true',
		LISTEN_ADDRESS: 'localhost',
		LISTEN_PORT: '1313',
		BASE_RESOURCE: "${webpackOutputDir}/development"
	)
}

eclipse {
	project {
		file.whenMerged {
			natures.remove('org.eclipse.wst.common.modulecore.ModuleCoreNature')
		}
	}
}
